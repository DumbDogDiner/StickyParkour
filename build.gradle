import kr.entree.spigradle.attribute.*

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'kr.entree.spigradle' version '1.2.4'
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'com.dumbdogdiner'
version '1.1.0'

description 'Interactive Minecraft Parkour plugin with a personality.'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()

    spigot()
    jitpack()

    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        name = "okkero"
        url = "https://nexus.okkero.com/repository/maven-releases/"
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'com.okkero.skedule:skedule:1.2.6'

    compileOnly paper("1.15.2")

    compileOnly vault()
    compileOnly 'me.clip:placeholderapi:2.10.6'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

tasks.build.dependsOn tasks.shadowJar
shadowJar {
    classifier = ''
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

spigot {
    authors = List.of("skyefoxie")
    softDepends = List.of("Vault", "PlaceholderAPI")
    apiVersion = "1.15"
    load = Load.STARTUP
    commands {
        create("parkour") {
            aliases = List.of("pk", "yapp")
            description = "Manage parkour courses"
            usage = "/parkour [list/create/modify/delete]"
        }
    }
    permissions {
        'parkour.command' {
            description = 'Allows players to create, edit, and delete courses'
            defaults = 'op'
        }
    }
}

// Bugfix for no referenced libraries when using Eclipse.
apply plugin: 'eclipse'

// Eclipse classpath bugfix
eclipse {
   classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
   }
}
