import kr.entree.spigradle.attribute.*

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'kr.entree.spigradle' version '1.2.4'
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

group 'com.dumbdogdiner'
version '1.1.0'

description 'Interactive Minecraft Parkour plugin with a personality.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    spigot()
    jitpack()

    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        name = "okkero"
        url = "http://nexus.okkero.com/repository/maven-releases/"
    }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'

    compile 'com.okkero.skedule:skedule:1.2.6'

    implementation paper("1.15.2")

    implementation vault()
    implementation 'me.clip:placeholderapi:2.10.6'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

jar {
    manifest { attributes 'Main-Class': 'Yapp.YappParkourPluginKt' }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

spigot {
    authors = List.of("skyefoxie")
    softDepends = List.of("Vault", "PlaceholderAPI")
    apiVersion = "1.15"
    load = Load.STARTUP
    commands {
        create("parkour") {
            aliases = List.of("pk", "yapp")
            description = "Manage parkour courses"
            usage = "/parkour [list/create/modify/delete]"
        }
    }
    permissions {
        'parkour.command' {
            description = 'Allows players to create, edit, and delete courses'
            defaults = 'op'
        }
    }
}

// Bugfix for no referenced libraries when using Eclipse.
apply plugin: 'eclipse'

// Eclipse classpath bugfix
eclipse {
   classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
   }
}